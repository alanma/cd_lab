apply plugin: 'maven-publish'

ext {
    nexusReleaseRepoUrl = config.binaryRepository.releaseUrl
    nexusUsername = project.hasProperty('nexusUsername') ? project.getProperty('nexusUsername') : null
    nexusPassword = project.hasProperty('nexusPassword') ? project.getProperty('nexusPassword') : null
}

publishing {
    publications {
        toDoWebApp(MavenPublication) {
            from components.web
            artifactId 'todo-web'

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'To Do application')
                root.appendNode('description', 'A simple task management application.')
            }
        }
    }

    repositories {
        maven {
            name 'remoteMavenRepo'
            url nexusReleaseRepoUrl

            credentials {
                username = nexusUsername                                 
                password = nexusPassword
            }
        }   
    }
}